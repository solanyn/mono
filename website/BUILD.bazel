load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_rules_js//js:defs.bzl", "js_run_binary")
load("@npm//website:astro/package_json.bzl", astro_bin = "bin")
load("@npm//:wrangler/package_json.bzl", wrangler_bin = "bin")
load("@npm//:defs.bzl", "npm_link_all_packages")

npm_link_all_packages()

copy_file(
    name = "resume_json",
    src = "//resume:resume_json",
    out = "resume/resume.json",
)

SRCS = [
    "package.json",
    "tsconfig.json",
    "astro.config.mjs",
    ":resume_json",
] + glob([
    "src/**",
    "public/**",
])

BUILD_DEPS = [":node_modules/" + d for d in [
    "astro",
    "@astrojs/mdx",
    "@astrojs/rss",
    "@astrojs/sitemap",
    "sharp",
]]

astro_bin.astro_binary(
    name = "astro",
    chdir = package_name(),
    env = {
        "ASTRO_TELEMETRY_DISABLED": "1",
    },
)

js_run_binary(
    name = "build",
    srcs = SRCS + BUILD_DEPS,
    args = ["build"],
    env = {
        "PUBLIC_R2_URL": "https://assets.goyangi.io",
    },
    mnemonic = "AstroBuild",
    out_dirs = ["dist"],
    tool = ":astro",
)

astro_bin.astro_binary(
    name = "dev",
    chdir = package_name(),
    args = ["dev", "--host", "0.0.0.0"],
    env = {
        "ASTRO_TELEMETRY_DISABLED": "1",
    },
    data = SRCS + BUILD_DEPS,
)

wrangler_bin.wrangler_binary(
    name = "deploy",
    chdir = package_name(),
    args = [
        "pages",
        "deploy",
        "dist",
        "--project-name=website",
    ],
    data = [
        ":build",
        "//:node_modules/wrangler",
        "wrangler.toml",
    ] + glob(["functions/**"]),
)
